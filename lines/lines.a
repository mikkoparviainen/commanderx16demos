!to "lines.prg", cbm
!cpu 65c02

!source "../includes/system.inc"
!source "../includes/graphics.inc"

GETIN=$ffe4

PTR=$20

*=$0801
	+basic_sys

	jsr vera_init_320x240_8bpp ; set 8bpp bitmap mode
	lda #01
	sta colour
	jsr clear_screen

	lda #00
	sta colour
	ldy #10
	lda #$01
	sta hline_len+1 ; draw long horizontal lines

-	lda #0
	sta y+1
	sta x
	sta x+1
	tya
	sta y
	sta hline_len
	jsr hline
	dey
	bne -

-	jsr GETIN
	cmp #00
	beq -

	lda #01
	sta colour
	jsr clear_screen

	lda #06
	sta colour
	lda #$0F
	sta y
	lda #$10
	sta x
	lda #00
	sta y+1
	sta x+1 ; pixel one above start of line on start x
	jsr draw_pixel
	lda #$20
	sta x
	jsr draw_pixel ; pixel one above start line on stop x
	lda #$14
	sta y
	jsr draw_pixel ; pixel one above stop of line

	lda #00
	sta colour

	lda #<lines
	sta PTR
	lda #>lines
	sta PTR+1

	ldy #0

-	lda (PTR),y
	cmp #$FF
	beq + ; we're done here
	sta x0
	iny
	lda (PTR),y
	sta x0+1
	iny
	lda (PTR),y
	sta y0
	iny
	lda (PTR),y
	sta y0+1
	iny

	lda (PTR),y
	sta x1
	iny
	lda (PTR),y
	sta x1+1
	iny
	lda (PTR),y
	sta y1
	iny
	lda (PTR),y
	sta y1+1
	iny

	jsr line
	jmp -
+
-	jsr GETIN
	cmp #00
	beq -

	jsr vera_reset
	rts

; the list of lines to draw. Max length needs to be less than 255 bytes
; because we use the y register to point to these
lines
	!word $0010, $0010, $0020, $0015 ; ($10, $10) - ($20, $15) - increasing y
	!word $0010, $0028, $0064, $0028 ; ($10, $28) - ($64, $28) - zero y
	!word $0010, $0020, $0020, $0018 ; ($10, $20) - ($20, $18) - decreasing y
	!word $FFFF ; end marker
